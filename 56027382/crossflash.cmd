@ECHO OFF
SETLOCAL
SET COMM=1
::
SET /P COMM=Set comm port number 1-9 ^> 
ECHO %COMM%|FINDSTR /R "[1-9]" >nul
IF ERRORLEVEL 1 GOTO ERROR
::
..\SBEC_utils\Release\SBEC_chipid.exe /C:%COMM%
:: ST
IF %ERRORLEVEL% EQU 8360 GOTO FLASH_ST
IF %ERRORLEVEL% EQU 8194 GOTO FLASH_ST
:: AMD
IF %ERRORLEVEL% EQU 417 GOTO FLASH_ST
IF %ERRORLEVEL% EQU 293 GOTO FLASH_ST
:: Intel
IF %ERRORLEVEL% EQU 35257 GOTO FLASH_ST
IF %ERRORLEVEL% EQU 35256 GOTO FLASH_ST
:: Toshiba
IF %ERRORLEVEL% EQU 38976 GOTO FLASH_TOSH
IF %ERRORLEVEL% EQU 39024 GOTO FLASH_TOSH
GOTO ERROR
::
:FLASH_ST
..\SBEC_utils\Release\SBEC_erase_st.exe /C:%COMM% /B
IF %ERRORLEVEL% NEQ 0 GOTO ERROR
::
..\SBEC_utils\Release\SBEC_flash_st.exe /C:%COMM% /F:"56027382_v8.bin" /B
IF %ERRORLEVEL% NEQ 0 GOTO ERROR
GOTO QUIT
::
:FLASH_TOSH
..\SBEC_utils\Release\SBEC_erase_tosh.exe /C:%COMM% /B
IF %ERRORLEVEL% NEQ 0 GOTO ERROR
::
..\SBEC_utils\Release\SBEC_flash_tosh.exe /C:%COMM% /F:"56027382_v8.bin" /B
IF %ERRORLEVEL% NEQ 0 GOTO ERROR
GOTO QUIT
::
:ERROR
ECHO Flash fail
PAUSE
EXIT 1
::
:QUIT
ECHO Flash success
PAUSE
EXIT 0